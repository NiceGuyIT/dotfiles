#!/usr/bin/env nu

# This script will create a desktop icon for every Firefox profile on Linux.
# {{ $profiles := joinPath .chezmoi.homeDir ".mozilla/firefox/profiles.ini" }}
# {{ if stat $profiles }}{{ include $profiles | sha256sum }}{{ end }}

let desktop_file = "/usr/share/applications/firefox.desktop"
let firefox_dir = ($env.HOME | path join '.mozilla/firefox')
let firefox_bin = (which firefox)
let profiles = ($firefox_dir | path join 'profiles.ini')

if ($desktop_file | is-empty) {
	print $"Sample desktop file not found: ($desktop_file)"
	exit 0
}
if ($firefox_dir | is-empty) {
	print $"Firefox config directory not found: ($firefox_dir)"
	exit 0
}
if ($firefox_bin | is-empty) {
	print $"Firefox binary was not found in path: ($firefox_bin)"
	exit 0
}
if ($profiles | is-empty) {
	print $"Firefox profiles was not found in config: ($profiles)"
	exit
}

let profile_paths = (open $profiles
	| transpose section value
	| where section =~ "Profile"
	| where value.Path !~ "default"
	| select section value.Path
	| insert name {|it|
		$it.value_Path
			| split column '.'
			| get 0.column2
	}
)

$profile_paths | each {|it|
	let filename = $"firefox-($it.name).desktop"
	open $desktop_file
		| lines
		| str replace --regex '^Exec=(.*) %u' $"Exec=$1 -P \"($it.name)\" %u"
		| str replace --regex '^Name(.*)=(.*)' $"Name$1=$2 \(($it.name)\)"
		| to text
		| save --force $filename
	print $"Saving ($filename)"
}
