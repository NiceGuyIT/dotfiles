#!/usr/bin/env nu

# This script will create a desktop icon for every Firefox profile.
# profiles.ini hash: {{ include (joinPath .chezmoi.homeDir ".mozilla/firefox/profiles.ini") | sha256sum }}

let desktop_file = "/usr/share/applications/firefox.desktop"
let firefox_dir = "~/.mozilla/firefox"
let firefox_bin = (which firefox)

let profiles = ($firefox_dir | path join "profiles.ini")
let profile_paths = (open $profiles
	| transpose section value
	| where section =~ "Profile"
	| where value.Path !~ "default"
	| select section value.Path
	| insert name {|it|
		$it.value_Path
			| split column '.'
			| get 0.column2
	}
)

$profile_paths | each {|it|
	let filename = $"firefox-($it.name).desktop"
	open $desktop_file
		| lines
		| str replace --regex '^Exec=(.*) %u' $"Exec=$1 -P \"($it.name)\" %u"
		| str replace --regex '^Name(.*)=(.*)' $"Name$1=$2 \(($it.name)\)"
		| to text
		| save --force $filename
	print $"Saving ($filename)"
}
